import win.ui;
import fsys
import dlm //按照aauto提供的多线程下载管理器所修改的版本, 原版本的cookie是分开的。
import win
import web.form;
import inet.http;


//窗口绘制
/*DSG{{*/
var winform = ..win.form(text="Web Form";right=1024;bottom=768)
winform.add(
button={cls="button";text="- download -";left=912;top=24;right=992;bottom=56;flat=1;tabstop=1;z=4};
combobox={cls="combobox";left=744;top=28;right=848;bottom=52;edge=1;items={};mode="dropdownlist";tabstop=1;z=10};
custom={cls="custom";text="custom";left=16;top=616;right=1009;bottom=750;ah=1;autosize=1;aw=1;center=1;edge=1;transparent=1;z=1};
groupbox={cls="groupbox";text="设置";left=16;top=8;right=1008;bottom=72;edge=1;z=3};
listview={cls="listview";left=16;top=80;right=1009;bottom=603;ah=1;aw=1;bgcolor=16777215;edge=1;fullRow=1;gridLines=1;z=2};
pwdEdit={cls="edit";left=408;top=28;right=632;bottom=52;edge=1;tabstop=1;z=8};
static2={cls="static";text="账号";left=40;top=32;right=96;bottom=48;transparent=1;z=5};
static3={cls="static";text="密码";left=352;top=32;right=415;bottom=55;transparent=1;z=6};
static4={cls="static";text="分类";left=688;top=32;right=751;bottom=55;transparent=1;z=9};
usrEdit={cls="edit";left=96;top=29;right=320;bottom=53;edge=1;tabstop=1;z=7}
)
/*}}*/

winform.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) { 
		case 0x10/*_WM_CLOSE*/{
			import process
    		process.TerminateProcess(process.getHandle(),0)
    }
	}
	//无返回值则继续调用默认回调函数
}


//列表框设定
winform.listview.insertColumn("网址",180);
winform.listview.insertColumn("文件名",180);
winform.listview.insertColumn("状态",100);
winform.listview.insertColumn("大小",80);
winform.listview.insertColumn("速度",80);
winform.listview.insertColumn("已下载",120);
winform.listview.adjust = function(cx,cy){winform.listview.fillParent(1);}

winform.combobox.add("全部")
winform.combobox.add("收件箱")
winform.combobox.add("发件箱")
winform.combobox.selText = "全部"


//定义变量
times = 0 //循环次数, 用于命名文件名
works = 0 //同时下载数
threadamount = 10 //线程上限
looptime = 0 //外圈循环，为了让程序有足够时间处理窗口列表框用

dlmg = dlm.dlManager(threadamount) //多线程下载管理器对象

	

// download按钮事件. 主要代码都在这.
winform.button.oncommand = function(id,event){
	targetfid = 0 //这是组合框用变量。用于分门别类下载邮件
	emailtype = "" //邮箱类型 (163, 126)
	
	
	// 组合框判断
	select(winform.combobox.selText){
		case "全部" {targetfid = 0}
		case "收件箱" {targetfid = 1}
		case "发件箱" {targetfid = 3}
		}
	
	//定义web.form登录网易邮箱
	var wb = web.form( winform.custom ,/*_UIFLAG_*/,/*_DLCTL_*/,/*USER AGENT*/,true/*securityTrusted*/ );
	wb.external = { 
		func = function( num ){
			winform.text = ""
		}
	}
	
	//判断邮箱类型 (163, 126)
	temp = winform.usrEdit.text;
	
	if (..string.find(temp,"126")){emailtype = "126"}
	elseif(..string.find(temp, "163")){emailtype = "163"}
	
	// 打开登录页面
	if (emailtype == "126"){wb.go("http://www.126.com") }
	elseif(emailtype == "163"){wb.go("http://mail.163.com") }
	
	wb.wait("");
	
	//网页填表
	wb.getEle("idInput").value = string.match(temp,"(\w+)@")
	wb.getEle("pwdInput").value = winform.pwdEdit.text;
	wb.getEle("loginBtn").click(); //点击登录按钮
	wb.wait("main.jsp")

	
	htmlcontent = wb.body.innerHTML //获取邮箱界面源代码
	sid = ..string.match(htmlcontent, "sid=\w+")
	sid = ..string.sub(sid,5) //寻获sid, 用于获取邮件列表

	if (emailtype == "126"){emailList = 'http://mail.126.com/js6/s?sid='++ sid ++'&func=mbox:listMessages'} //合成邮件列表的网址
	elseif(emailtype == "163"){emailList = 'http://mail.163.com/js6/s?sid='++ sid ++'&func=mbox:listMessages'}
	
	//获取邮件列表
	http = inet.http()
	htmlcontent = http.get(emailList)
	htmlcontent = string.fromto(htmlcontent) //转换一下， 避免乱码

	midmatch = string.gmatch(htmlcontent ,  '<@<string name="id">@>(.{20,30})<@</string>@>') //fid 是邮箱文件夹的id,即 folderid, mid为邮件id, 即mailid
	fidmatch = string.gmatch(htmlcontent , '<@<int name="fid">@>(\\d{1})<@</int>@>' )
	while (1){
		win.delay(50)	
		if (works == 0 ) // 如果同时下载的数量为0
		{	
			if (winform.listview.count >= 50){winform.listview.clear()} //如果列表框中项目超过50个则清除
			win.delay(50)	
			while(looptime < threadamount){ //循环n次下载
				looptime ++
				win.delay(50)
				times ++
				mid = midmatch()
				fid = fidmatch()
				
				if (targetfid == fid || targetfid == 0){ //目标文件夹判断处理
					if (mid){
						if(emailtype == "126") {downloadUrl = 'http://mail.126.com/js6/read/readdata.jsp?sid=' ++ sid ++ '&mode=download&mid='++ mid ++'&l=inbox&action=export'}
						elseif(emailtype == "163"){downloadUrl = 'http://mail.163.com/js6/read/readdata.jsp?sid=' ++ sid ++ '&mode=download&mid='++ mid ++'&l=inbox&action=export'}
						dlmg.push(id=winform.listview.addItem(downloadUrl);url=downloadUrl;filename=tostring(times) ++ ".eml";savedir="/mail/") //推送下载任务到多线程下载管理器
						works ++ //同时下载数量加1
						win.delay(50)
						}
					else {return}
					win.delay(50)		
					}
				}
			looptime = 0
		}
			win.delay(50)	
	}		
	


}



//多线程下载用回调函数

dlmg.onEnd = function(id,savepath,resumePath,contentLength){
	if( savepath ){ 
        winform.listview.setItemText(  "已完成"  ,id,3);
        winform.listview.setItemText( fsys.formatSize(contentLength),id,4);
        works = works - 1
        
    }
    else {
        winform.listview.setItemText(  "已停止"  ,id,3);
    }
    winform.listview.setItemText(  "0KB/s"  ,id,5); 
    
}

dlmg.onReceiveBegin = function(id,url,filename,statusText,httpStatusCode,totalSize,downSize){
    winform.listview.setItemText( {url;filename;statusText;fsys.formatSize(totalSize);fsys.formatSize(downSize) },id )
}

dlmg.onReceive = function(id,sizePs,downSize){
    winform.listview.setItemText( fsys.formatSize(downSize),id,6);
    winform.listview.setItemText( fsys.formatSize(sizePs) + "/s" ,id,5);

}

dlmg.onError = function(id,err){
    winform.listview.setItemText( err,id,3);
    works = works - 1
}

winform.show(); 
win.loopMessage();